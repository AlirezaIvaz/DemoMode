apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.franco.demomode"
        minSdkVersion 24
        targetSdkVersion 33
        versionName "1.6"
        versionCode 1910030257
        archivesBaseName = "DemoModeTile-${android.defaultConfig.versionName}"
    }

    signingConfigs {
        release {
            readKeystoreProperties('release')
        }

        debug {
            readKeystoreProperties('debug')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        checkAllWarnings false
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            shrinkResources false
        }
    }

    buildFeatures {
        viewBinding = true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

def readKeystoreProperties(def configName) {
    def propsFile = rootProject.file('../../keystore.properties')

    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        android.signingConfigs[configName].storeFile = file(props['storeFile'])
        android.signingConfigs[configName].storePassword = props['storePassword']
        android.signingConfigs[configName].keyAlias = props['keyAlias']
        android.signingConfigs[configName].keyPassword = props['keyPassword']
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "androidx.fragment:fragment-ktx:1.2.5"
    implementation "androidx.core:core-ktx:1.3.1"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'

    def lifecycle_version = "2.3.0-alpha07"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
}

repositories {
    mavenCentral()
}
